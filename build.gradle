plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.32")
    annotationProcessor("org.projectlombok:lombok:1.18.32")
}

subprojects {
    // Only configure projects that use maven-publish
    plugins.withId('maven-publish') {
        publishing {
            repositories {
                maven {
                    name = "nexus"

                    def snapshotsUrl = findProperty("nexusSnapshotsUrl") ?: "https://repo.negative.games/repository/maven-snapshots"
                    def releasesUrl  = findProperty("nexusReleasesUrl")  ?: "https://repo.negative.games/repository/maven-releases"

                    // Controlled via -PisRelease=true|false (or default to snapshots)
                    def isRelease = (findProperty("isRelease") == "true")
                    url = uri(isRelease ? releasesUrl : snapshotsUrl)

                    credentials {
                        username = findProperty("nexusUsername")
                        password = findProperty("nexusPassword")
                    }
                }
            }
        }
    }
}
