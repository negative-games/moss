plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
}

def id = "MyPlugin"
group = 'com.example.myplugin'
version = '1.0.0'

repositories {
    mavenCentral()

    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }

    maven {
        url = uri("https://libraries.minecraft.net")
    }

    maven {
        name = "negative-games-repo"
        url = "https://repo.negative.games/repository/maven-snapshots/"
    }
}

dependencies {
    compileOnly("net.md-5:bungeecord-api:1.20-R0.2")

    // IMPORTANT: We need to compile Spring Context for this to work!
    implementation("org.springframework:spring-context:6.2.13")

    // Moss (Bungeecord Platform)
    implementation("games.negative.moss:moss-bungeecord:1.0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {
    build {
        dependsOn(shadowJar)
    }
}

shadowJar {
    archiveBaseName.set("${id}")
    archiveClassifier.set("")
    archiveVersion.set("")

    relocate("games.negative.moss", "${group}.libs.moss")
    // relocate other dependencies such as Spring
}