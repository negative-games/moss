plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
}

def id = "MyPlugin"
group = 'com.example.myplugin'
version = '1.0.0'

repositories {
    mavenCentral()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        name = "negative-games-repo"
        url = "https://repo.negative.games/repository/maven-snapshots/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")

    // IMPORTANT: We need to compile Spring Context for this to work!
    implementation("org.springframework:spring-context:6.2.13")

    // Moss (PaperMC Platform)
    implementation("games.negative.moss:moss-paper:1.0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

tasks {
    build {
        dependsOn(shadowJar)
    }
}

shadowJar {
    archiveBaseName.set("${id}")
    archiveClassifier.set("")
    archiveVersion.set("")

    relocate("games.negative.moss", "${group}.libs.moss")
    // relocate other dependencies such as Spring
}