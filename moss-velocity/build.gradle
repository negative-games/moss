plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.2.2'
}

def id = 'moss-velocity'
def domain = 'games.negative.moss'
def apiVersion = '1.1.0'

repositories {
    mavenCentral()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    implementation(project(":moss"))

    compileOnly("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")

    // https://mvnrepository.com/artifact/org.springframework/spring-context
    compileOnly("org.springframework:spring-context:6.2.13")

    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.32")
    annotationProcessor("org.projectlombok:lombok:1.18.32")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.jar {
    enabled = false
}

tasks.shadowJar {
    archiveBaseName.set(id)
    archiveVersion.set(apiVersion)
    archiveClassifier.set("")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                builtBy tasks.shadowJar
            }

            // Explicit coordinates (optional, defaults to project.group/name/version)
            groupId = "${domain}"
            artifactId = "${id}"
            version = "${apiVersion}"

            pom {
                name = "${id}"
                description = project.description
                url = 'https://github.com/negative-games/moss'

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'ericlmao'
                        name = 'Eric'
                    }
                }
            }
        }
    }
}